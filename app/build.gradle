apply plugin: 'com.android.application'

def static getDoubanApiKey() {
    def Properties props = new Properties()
    props.load(new FileInputStream(new File('apiKeys.properties')))
    byte[] data = props['doubanApiKey'].getBytes("UTF-8")
    data[0] = data[0] + 1
    data[1] = data[1] - 2 // just for obfuscation
    String base64 = Base64.getEncoder().encodeToString(data)
    return base64
}

def static getJikeApiKey() {
    def Properties props = new Properties()
    props.load(new FileInputStream(new File('apiKeys.properties')))
    byte[] data = props['jikeApiKey'].getBytes("UTF-8")
    String base64 = Base64.getEncoder().encodeToString(data)
    return base64
}

android {
    compileSdkVersion project.CompileSdkVersion
    buildToolsVersion project.BuildToolsVersion
    defaultConfig {
        applicationId "com.smartjinyu.mybookshelf"
        minSdkVersion 21
        targetSdkVersion project.TargetSdkVersion
        versionCode 17
        versionName "1.7"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField 'String', "doubanApiKey", "\"" + getDoubanApiKey() + "\""
            buildConfigField 'String', "jikeApiKey", "\"" + getJikeApiKey() + "\""
        }
        debug {
            buildConfigField 'String', "doubanApiKey", "\"" + getDoubanApiKey() + "\""
            buildConfigField 'String', "jikeApiKey", "\"" + getJikeApiKey() + "\""
        }
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('androidx.test.espresso:espresso-core:3.4.0-alpha01', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'androidx.palette:palette:1.0.0'
    testImplementation 'junit:junit:4.13.1'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'com.github.clans:fab:1.6.4' // discontinued project
    implementation 'me.dm7.barcodescanner:zxing:1.9.13' // discontinued project
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0' // newest 3.3.0 // original 0.9.6.0
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.klinkerapps:sliding-activity:1.5.2'
    implementation 'com.mikepenz:materialdrawer:6.1.2'
    implementation 'com.github.promeg:tinypinyin:2.0.3'
    implementation project(':compressor')
    implementation('com.squareup.retrofit2:converter-simplexml:2.9.0') {
        exclude group: 'xpp3', module: 'xpp3'
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'stax', module: 'stax'
    }
    implementation files('libs/opencsv-3.9.jar')

}
